in_source: |
  port:
                  word 1
  hello_str:
                  string "Hello, what is your name?\n"

  print_char:
                  lit port        ; symbol port_addr
                  load            ; symbol port
                  out             ;
                  ret             ;

  print_str:
                  dup                 ; str_addr str_addr
                  load                ; str_addr length
  print_loop:
                  dup                 ; str_addr length length
                  lit print_end       ; str_addr length length print_end
                  swap                ; str_addr length print_end length
                  jz                  ; str_addr length

                  swap                ; length str_addr
                  inc                 ; length str_addr+1
                  over                ; str_addr+1 length str_addr+1
                  load                ; str_addr+1 length symbol

                  lit print_char      ; str_addr+1 length symbol print_char
                  call                ; str_addr+1 length
                  dec                 ; str_addr+1 length-1
                  lit print_loop      ; str_addr+1 length-1 print_loop
                  jump                ; str_addr+1 length-1
  print_end:
                  ret                 ;

  read_char:
                  lit port        ; port_addr
                  load            ; port
                  in              ; in_symbol
                  ret             ; in_symbol

  var_symbol:
                  word 0
  length_addr:
                  word 0

  read_string:
                  dup             ; str_addr str_addr
                  lit length_addr ; str_addr str_addr length_addr
                  store           ; str_addr
  read_loop:
                  lit read_char   ; str_addr read_char
                  call            ; str_addr symbol
                  dup             ; str_addr symbol symbol
                  lit end_read    ; str_addr symbol symbol end_read
                  swap            ; str_addr symbol end_read symbol
                  jz              ; str_addr symbol

                  lit length_addr ; str_addr symbol length_addr_addr
                  load            ; str_addr symbol length_addr
                  dup             ; str_addr symbol length_addr length_addr
                  load            ; str_addr symbol length_addr length

                  inc             ; str_addr symbol length_addr length+1
                  swap            ; str_addr symbol length+1 length_addr
                  store           ; str_addr symbol
                  swap            ; symbol str_addr
                  inc             ; symbol str_addr+1
                  over            ; str_addr+1 symbol str_addr+1
                  store           ; str_addr+1

                  lit read_loop   ; str_addr+1 read_loop
                  jump            ; str_addr+1
  end_read:
                  drop            ; str_addr
                  drop            ;
                  ret             ;

  start:
                  lit 62          ; '>'
                  lit print_char  ; '>' print_char
                  call            ;
                  lit 32          ; ' '
                  lit print_char  ; ' ' print_char
                  call            ;

                  lit hello_str   ; hello_str
                  lit print_str   ; hello_str print_str
                  call            ;

                  lit user_input  ; user_input
                  lit read_string ; user_input read_string
                  call            ;

                  lit 60          ; '<'
                  lit print_char  ; '<' print_char
                  call            ;
                  lit 32          ; ' '
                  lit print_char  ; ' ' print_char
                  call            ;

                  lit user_input  ; user_input
                  lit print_str   ; user_input print_str
                  call            ;

                  lit 10          ; '\n'
                  lit print_char  ; '\n' print_char
                  call            ;

                  lit 62          ; '>'
                  lit print_char  ; '>' print_char
                  call            ;
                  lit 32          ; ' '
                  lit print_char  ; ' ' print_char
                  call            ;

                  lit hello_user  ; hello_user
                  lit print_str   ; hello_user print_str
                  call            ;

                  lit user_input  ; user_input
                  lit print_str   ; user_input print_str
                  call

                  lit 33          ; '!'
                  lit print_char  ; '!' print_char
                  call            ;

                  halt            ;

  user_input:
                  buf 30

  hello_user:
                  string "Hello, "

in_stdin: rmntim
out_stdout: |-
  > Hello, what is your name?
  < rmntim
  > Hello, rmntim!
